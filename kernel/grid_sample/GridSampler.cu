// Derived from PyTorch: aten/src/ATen/native/cuda/GridSampler.cu
// PyTorch v2.7.1, commit e2d141dbde55c2a4370fac5165b0561b6af4798b.
// SPDX-License-Identifier: BSD-3-Clause
// Copyright 2026 Hygon Information Technology Co., Ltd.
// Modified by Hygon.
#include <cuda.h>
#include <cuda_runtime.h>

#include <torch/extension.h>
#include <ATen/OpMathType.h>
#include <ATen/cuda/CUDAContext.h>
#include <ATen/cuda/detail/TensorInfo.cuh>
#include <ATen/cuda/detail/IndexUtils.cuh>
#include <ATen/cuda/detail/KernelUtils.h>
#include <ATen/core/TensorBase.h>
#include <ATen/Dispatch.h>
#include <c10/macros/Macros.h>
#include <GridSampler.cuh>

#include <limits>
#include <cmath>

namespace at {
namespace native {

using namespace at::cuda::detail;

using at::native::detail::GridSamplerInterpolation;
using at::native::detail::GridSamplerPadding;


template <typename scalar_t, typename index_t>
C10_LAUNCH_BOUNDS_1(256)
__global__ void grid_sampler_2d_impl_kernel(
    const index_t nthreads,
    TensorInfo<const scalar_t, index_t> input,
    TensorInfo<const scalar_t, index_t> grid,
    TensorInfo<scalar_t, index_t> output,
    const GridSamplerInterpolation interpolation_mode,
    const GridSamplerPadding padding_mode,
    bool align_corners) {

  using opmath_t = at::opmath_type<scalar_t>;
  index_t C = input.sizes[1];
  index_t inp_H = input.sizes[2];
  index_t inp_W = input.sizes[3];
  index_t out_H = grid.sizes[1];
  index_t out_W = grid.sizes[2];
  index_t inp_sN = input.strides[0];
  index_t inp_sC = input.strides[1];
  index_t inp_sH = input.strides[2];
  index_t inp_sW = input.strides[3];
  index_t grid_sN = grid.strides[0];
  index_t grid_sH = grid.strides[1];
  index_t grid_sW = grid.strides[2];
  index_t grid_sCoor = grid.strides[3];
  index_t out_sN = output.strides[0];
  index_t out_sC = output.strides[1];
  index_t out_sH = output.strides[2];
  index_t out_sW = output.strides[3];

  CUDA_KERNEL_LOOP_TYPE(index, nthreads, index_t) {
    const index_t w = index % out_W;
    const index_t h = (index / out_W) % out_H;
    const index_t n = index / (out_H * out_W);
    const index_t grid_offset = n * grid_sN + h * grid_sH + w * grid_sW;

    // get the corresponding input x, y coordinates from grid
    opmath_t x = grid.data[grid_offset];
    opmath_t y = grid.data[grid_offset + grid_sCoor];

    opmath_t ix = grid_sampler_compute_source_index(x, inp_W, padding_mode, align_corners);
    opmath_t iy = grid_sampler_compute_source_index(y, inp_H, padding_mode, align_corners);

    if (interpolation_mode == GridSamplerInterpolation::Bilinear) {
      // get NE, NW, SE, SW pixel values from (x, y)
      index_t ix_nw = static_cast<index_t>(::floor(ix));
      index_t iy_nw = static_cast<index_t>(::floor(iy));
      index_t ix_ne = ix_nw + 1;
      index_t iy_ne = iy_nw;
      index_t ix_sw = ix_nw;
      index_t iy_sw = iy_nw + 1;
      index_t ix_se = ix_nw + 1;
      index_t iy_se = iy_nw + 1;

      // get surfaces to each neighbor:
      opmath_t nw = (ix_se - ix)    * (iy_se - iy);
      opmath_t ne = (ix    - ix_sw) * (iy_sw - iy);
      opmath_t sw = (ix_ne - ix)    * (iy    - iy_ne);
      opmath_t se = (ix    - ix_nw) * (iy    - iy_nw);

      // calculate bilinear weighted pixel value and set output pixel
      auto inp_ptr_NC = input.data + n * inp_sN;
      auto out_ptr_NCHW = output.data + n * out_sN + h * out_sH + w * out_sW;
      for (index_t c = 0; c < C; ++c, inp_ptr_NC += inp_sC, out_ptr_NCHW += out_sC) {
        opmath_t out_acc = 0;
        if (within_bounds_2d(iy_nw, ix_nw, inp_H, inp_W)) {
          out_acc += inp_ptr_NC[iy_nw * inp_sH + ix_nw * inp_sW] * nw;
        }
        if (within_bounds_2d(iy_ne, ix_ne, inp_H, inp_W)) {
          out_acc += inp_ptr_NC[iy_ne * inp_sH + ix_ne * inp_sW] * ne;
        }
        if (within_bounds_2d(iy_sw, ix_sw, inp_H, inp_W)) {
          out_acc += inp_ptr_NC[iy_sw * inp_sH + ix_sw * inp_sW] * sw;
        }
        if (within_bounds_2d(iy_se, ix_se, inp_H, inp_W)) {
          out_acc += inp_ptr_NC[iy_se * inp_sH + ix_se * inp_sW] * se;
        }
        *out_ptr_NCHW = out_acc;
      }
    } 
  }
}

// NHWC 专用 kernel：通道向量化 (float4) + 2D block 增强 (h,w) 空间 locality
template <typename scalar_t, typename index_t>
C10_LAUNCH_BOUNDS_2(256, 1)  // 16x16 block
__global__ void grid_sampler_2d_impl_kernel_nhwc(
    index_t out_H,
    index_t out_W,
    TensorInfo<scalar_t, index_t> input,
    TensorInfo<scalar_t, index_t> grid,
    TensorInfo<scalar_t, index_t> output,
    const GridSamplerInterpolation interpolation_mode,
    const GridSamplerPadding padding_mode,
    bool align_corners) {

  using opmath_t = at::opmath_type<scalar_t>;
  index_t C = input.sizes[1];
  index_t inp_H = input.sizes[2];
  index_t inp_W = input.sizes[3];
  index_t inp_sN = input.strides[0];
  index_t inp_sH = input.strides[2];
  index_t inp_sW = input.strides[3];
  index_t grid_sN = grid.strides[0];
  index_t grid_sH = grid.strides[1];
  index_t grid_sW = grid.strides[2];
  index_t grid_sCoor = grid.strides[3];
  index_t out_sN = output.strides[0];
  index_t out_sH = output.strides[2];
  index_t out_sW = output.strides[3];

  constexpr int VEC = 4;
  const bool use_float4 = (sizeof(scalar_t) == sizeof(float) && (C % VEC) == 0);

  // 2D block: 同 block 内 (h,w) 相邻，采样 (ix,iy) 也倾向相邻，提升 L1/L2 命中
  const index_t w = blockIdx.x * blockDim.x + threadIdx.x;
  const index_t h = blockIdx.y * blockDim.y + threadIdx.y;
  const index_t n = blockIdx.z;

  if (h >= out_H || w >= out_W) return;

  const index_t grid_offset = n * grid_sN + h * grid_sH + w * grid_sW;

  opmath_t x = grid.data[grid_offset];
  opmath_t y = grid.data[grid_offset + grid_sCoor];

  opmath_t ix = grid_sampler_compute_source_index(x, inp_W, padding_mode, align_corners);
  opmath_t iy = grid_sampler_compute_source_index(y, inp_H, padding_mode, align_corners);

  if (interpolation_mode == GridSamplerInterpolation::Bilinear) {
    index_t ix_nw = static_cast<index_t>(::floor(ix));
    index_t iy_nw = static_cast<index_t>(::floor(iy));
    index_t ix_ne = ix_nw + 1;
    index_t iy_ne = iy_nw;
    index_t ix_sw = ix_nw;
    index_t iy_sw = iy_nw + 1;
    index_t ix_se = ix_nw + 1;
    index_t iy_se = iy_nw + 1;

    opmath_t nw = (ix_se - ix)    * (iy_se - iy);
    opmath_t ne = (ix    - ix_sw) * (iy_sw - iy);
    opmath_t sw = (ix_ne - ix)    * (iy    - iy_ne);
    opmath_t se = (ix    - ix_nw) * (iy    - iy_nw);

    auto inp_base = input.data + n * inp_sN;
    auto out_ptr = output.data + n * out_sN + h * out_sH + w * out_sW;

    bool in_nw = within_bounds_2d(iy_nw, ix_nw, inp_H, inp_W);
    bool in_ne = within_bounds_2d(iy_ne, ix_ne, inp_H, inp_W);
    bool in_sw = within_bounds_2d(iy_sw, ix_sw, inp_H, inp_W);
    bool in_se = within_bounds_2d(iy_se, ix_se, inp_H, inp_W);

    if (use_float4) {
      for (index_t c = 0; c < C; c += VEC) {
        float4 v_nw = make_float4(0, 0, 0, 0);
        float4 v_ne = make_float4(0, 0, 0, 0);
        float4 v_sw = make_float4(0, 0, 0, 0);
        float4 v_se = make_float4(0, 0, 0, 0);

        if (in_nw) {
          v_nw = *reinterpret_cast<const float4*>(inp_base + iy_nw * inp_sH + ix_nw * inp_sW + c);
        }
        if (in_ne) {
          v_ne = *reinterpret_cast<const float4*>(inp_base + iy_ne * inp_sH + ix_ne * inp_sW + c);
        }
        if (in_sw) {
          v_sw = *reinterpret_cast<const float4*>(inp_base + iy_sw * inp_sH + ix_sw * inp_sW + c);
        }
        if (in_se) {
          v_se = *reinterpret_cast<const float4*>(inp_base + iy_se * inp_sH + ix_se * inp_sW + c);
        }

        float4 v_out;
        v_out.x = v_nw.x * nw + v_ne.x * ne + v_sw.x * sw + v_se.x * se;
        v_out.y = v_nw.y * nw + v_ne.y * ne + v_sw.y * sw + v_se.y * se;
        v_out.z = v_nw.z * nw + v_ne.z * ne + v_sw.z * sw + v_se.z * se;
        v_out.w = v_nw.w * nw + v_ne.w * ne + v_sw.w * sw + v_se.w * se;

        *reinterpret_cast<float4*>(out_ptr + c) = v_out;
      }
    } else {
      for (index_t c = 0; c < C; ++c) {
        opmath_t out_acc = 0;
        index_t off = c;
        if (in_nw) out_acc += inp_base[iy_nw * inp_sH + ix_nw * inp_sW + off] * nw;
        if (in_ne) out_acc += inp_base[iy_ne * inp_sH + ix_ne * inp_sW + off] * ne;
        if (in_sw) out_acc += inp_base[iy_sw * inp_sH + ix_sw * inp_sW + off] * sw;
        if (in_se) out_acc += inp_base[iy_se * inp_sH + ix_se * inp_sW + off] * se;
        out_ptr[c] = static_cast<scalar_t>(out_acc);
      }
    }
  }
}

// 反向 NHWC 专用 kernel：通道 float4 向量化
template <typename scalar_t, typename index_t>
C10_LAUNCH_BOUNDS_2(512, 1)
__global__ void grid_sampler_2d_backward_impl_kernel_nhwc(
    index_t out_H,
    index_t out_W,
    TensorInfo<scalar_t, index_t> grad_output,
    TensorInfo<scalar_t, index_t> input,
    TensorInfo<scalar_t, index_t> grid,
    TensorInfo<scalar_t, index_t> grad_input,
    TensorInfo<scalar_t, index_t> grad_grid,
    const GridSamplerInterpolation interpolation_mode,
    const GridSamplerPadding padding_mode,
    bool align_corners,
    const index_t grad_input_memory_span,
    const bool input_requires_grad) {

  index_t C = input.sizes[1];
  index_t inp_H = input.sizes[2];
  index_t inp_W = input.sizes[3];
  index_t inp_sN = input.strides[0];
  index_t inp_sH = input.strides[2];
  index_t inp_sW = input.strides[3];
  index_t grid_sN = grid.strides[0];
  index_t grid_sH = grid.strides[1];
  index_t grid_sW = grid.strides[2];
  index_t grid_sCoor = grid.strides[3];
  index_t gOut_sN = grad_output.strides[0];
  index_t gOut_sH = grad_output.strides[2];
  index_t gOut_sW = grad_output.strides[3];
  index_t gInp_sN = grad_input.strides[0];
  index_t gInp_sH = grad_input.strides[2];
  index_t gInp_sW = grad_input.strides[3];
  index_t gGrid_sW = grad_grid.strides[2];

  constexpr int VEC = 4;
  constexpr int VEC_W = 4;  // W 方向向量化：每线程处理 4 个相邻 w，grid 向量化读取
  const bool use_float4 = (sizeof(scalar_t) == sizeof(float) && (C % VEC) == 0);

  const index_t w_base = (blockIdx.x * blockDim.x + threadIdx.x) * VEC_W;
  const index_t h_out = blockIdx.y * blockDim.y + threadIdx.y;
  const index_t n = blockIdx.z;

  if (h_out >= out_H) return;

  // 向量化加载 grid：相邻 VEC_W 个 w 时用 float4 x2 加载 (x0,y0,...,x3,y3)
  scalar_t x_arr[4] = {0}, y_arr[4] = {0};
  const bool can_vec_grid = (w_base + 3 < out_W && grid_sW == 2 && grid_sCoor == 1 &&
                              sizeof(scalar_t) == sizeof(float));
  if (can_vec_grid) {
    const float4 *grid_ptr = reinterpret_cast<const float4*>(
        grid.data + n * grid_sN + h_out * grid_sH + w_base * grid_sW);
    const float4 g0 = grid_ptr[0];
    const float4 g1 = grid_ptr[1];
    x_arr[0] = static_cast<scalar_t>(g0.x);  y_arr[0] = static_cast<scalar_t>(g0.y);
    x_arr[1] = static_cast<scalar_t>(g0.z);  y_arr[1] = static_cast<scalar_t>(g0.w);
    x_arr[2] = static_cast<scalar_t>(g1.x);  y_arr[2] = static_cast<scalar_t>(g1.y);
    x_arr[3] = static_cast<scalar_t>(g1.z);  y_arr[3] = static_cast<scalar_t>(g1.w);
  }

  #pragma unroll
  for (int v = 0; v < VEC_W; ++v) {
    const index_t w = w_base + v;
    if (w >= out_W) break;

    const index_t index = n * out_H * out_W + h_out * out_W + w;
    const index_t grid_offset = n * grid_sN + h_out * grid_sH + w * grid_sW;

    scalar_t x, y;
    if (can_vec_grid) {
      x = x_arr[v];
      y = y_arr[v];
    } else {
      x = grid.data[grid_offset];
      y = grid.data[grid_offset + grid_sCoor];
    }

    scalar_t gix_mult, giy_mult;
    scalar_t ix = grid_sampler_compute_source_index_set_grad(x, inp_W, padding_mode, align_corners, &gix_mult);
    scalar_t iy = grid_sampler_compute_source_index_set_grad(y, inp_H, padding_mode, align_corners, &giy_mult);

    if (interpolation_mode == GridSamplerInterpolation::Bilinear) {
      index_t ix_nw = static_cast<index_t>(std::floor(ix));
      index_t iy_nw = static_cast<index_t>(std::floor(iy));
      index_t ix_ne = ix_nw + 1;
      index_t iy_ne = iy_nw;
      index_t ix_sw = ix_nw;
      index_t iy_sw = iy_nw + 1;
      index_t ix_se = ix_nw + 1;
      index_t iy_se = iy_nw + 1;

      scalar_t nw = (ix_se - ix)    * (iy_se - iy);
      scalar_t ne = (ix    - ix_sw) * (iy_sw - iy);
      scalar_t sw = (ix_ne - ix)    * (iy    - iy_ne);
      scalar_t se = (ix    - ix_nw) * (iy    - iy_nw);

      bool in_nw = within_bounds_2d(iy_nw, ix_nw, inp_H, inp_W);
      bool in_ne = within_bounds_2d(iy_ne, ix_ne, inp_H, inp_W);
      bool in_sw = within_bounds_2d(iy_sw, ix_sw, inp_H, inp_W);
      bool in_se = within_bounds_2d(iy_se, ix_se, inp_H, inp_W);

      float gix = 0.f, giy = 0.f;
      float *gInp_ptr = reinterpret_cast<float*>(grad_input.data);
      const index_t base_N = n * gInp_sN;

      if (use_float4) {
        const float *inp_base = reinterpret_cast<const float*>(input.data + n * inp_sN);
        const float *gOut_base = reinterpret_cast<const float*>(grad_output.data + n * gOut_sN + h_out * gOut_sH + w * gOut_sW);

        for (index_t c = 0; c < C; c += VEC) {
          float4 v_gOut = *reinterpret_cast<const float4*>(gOut_base + c);

          float4 v_nw = make_float4(0, 0, 0, 0);
          float4 v_ne = make_float4(0, 0, 0, 0);
          float4 v_sw = make_float4(0, 0, 0, 0);
          float4 v_se = make_float4(0, 0, 0, 0);
          if (in_nw) v_nw = *reinterpret_cast<const float4*>(inp_base + iy_nw * inp_sH + ix_nw * inp_sW + c);
          if (in_ne) v_ne = *reinterpret_cast<const float4*>(inp_base + iy_ne * inp_sH + ix_ne * inp_sW + c);
          if (in_sw) v_sw = *reinterpret_cast<const float4*>(inp_base + iy_sw * inp_sH + ix_sw * inp_sW + c);
          if (in_se) v_se = *reinterpret_cast<const float4*>(inp_base + iy_se * inp_sH + ix_se * inp_sW + c);

          if (input_requires_grad) {
            floatx4 d_nw; d_nw[0] = nw * v_gOut.x; d_nw[1] = nw * v_gOut.y; d_nw[2] = nw * v_gOut.z; d_nw[3] = nw * v_gOut.w;
            floatx4 d_ne; d_ne[0] = ne * v_gOut.x; d_ne[1] = ne * v_gOut.y; d_ne[2] = ne * v_gOut.z; d_ne[3] = ne * v_gOut.w;
            floatx4 d_sw; d_sw[0] = sw * v_gOut.x; d_sw[1] = sw * v_gOut.y; d_sw[2] = sw * v_gOut.z; d_sw[3] = sw * v_gOut.w;
            floatx4 d_se; d_se[0] = se * v_gOut.x; d_se[1] = se * v_gOut.y; d_se[2] = se * v_gOut.z; d_se[3] = se * v_gOut.w;
            safe_add_2d_float4(gInp_ptr, iy_nw, ix_nw, gInp_sH, gInp_sW, inp_H, inp_W, base_N + c, d_nw, grad_input_memory_span);
            safe_add_2d_float4(gInp_ptr, iy_ne, ix_ne, gInp_sH, gInp_sW, inp_H, inp_W, base_N + c, d_ne, grad_input_memory_span);
            safe_add_2d_float4(gInp_ptr, iy_sw, ix_sw, gInp_sH, gInp_sW, inp_H, inp_W, base_N + c, d_sw, grad_input_memory_span);
            safe_add_2d_float4(gInp_ptr, iy_se, ix_se, gInp_sH, gInp_sW, inp_H, inp_W, base_N + c, d_se, grad_input_memory_span);
          }

          float coeff_nw = (iy_se - iy);
          float coeff_ne = (iy_sw - iy);
          float coeff_sw = (iy - iy_ne);
          float coeff_se = (iy - iy_nw);
          gix -= (v_nw.x * v_gOut.x + v_nw.y * v_gOut.y + v_nw.z * v_gOut.z + v_nw.w * v_gOut.w) * coeff_nw;
          gix += (v_ne.x * v_gOut.x + v_ne.y * v_gOut.y + v_ne.z * v_gOut.z + v_ne.w * v_gOut.w) * coeff_ne;
          gix -= (v_sw.x * v_gOut.x + v_sw.y * v_gOut.y + v_sw.z * v_gOut.z + v_sw.w * v_gOut.w) * coeff_sw;
          gix += (v_se.x * v_gOut.x + v_se.y * v_gOut.y + v_se.z * v_gOut.z + v_se.w * v_gOut.w) * coeff_se;

          float cx_nw = (ix_se - ix), cx_ne = (ix - ix_sw), cx_sw = (ix_ne - ix), cx_se = (ix - ix_nw);
          giy -= (v_nw.x * v_gOut.x + v_nw.y * v_gOut.y + v_nw.z * v_gOut.z + v_nw.w * v_gOut.w) * cx_nw;
          giy -= (v_ne.x * v_gOut.x + v_ne.y * v_gOut.y + v_ne.z * v_gOut.z + v_ne.w * v_gOut.w) * cx_ne;
          giy += (v_sw.x * v_gOut.x + v_sw.y * v_gOut.y + v_sw.z * v_gOut.z + v_sw.w * v_gOut.w) * cx_sw;
          giy += (v_se.x * v_gOut.x + v_se.y * v_gOut.y + v_se.z * v_gOut.z + v_se.w * v_gOut.w) * cx_se;
        }
      } else {
        scalar_t *gOut_ptr = grad_output.data + n * gOut_sN + h_out * gOut_sH + w * gOut_sW;
        scalar_t *inp_ptr = input.data + n * inp_sN;
        index_t NC_offset = base_N;

        for (index_t c = 0; c < C; ++c) {
          scalar_t gOut = gOut_ptr[c];
          if (input_requires_grad) {
            safe_add_2d(grad_input.data, iy_nw, ix_nw, gInp_sH, gInp_sW, inp_H, inp_W, nw * gOut, NC_offset, grad_input_memory_span);
            safe_add_2d(grad_input.data, iy_ne, ix_ne, gInp_sH, gInp_sW, inp_H, inp_W, ne * gOut, NC_offset, grad_input_memory_span);
            safe_add_2d(grad_input.data, iy_sw, ix_sw, gInp_sH, gInp_sW, inp_H, inp_W, sw * gOut, NC_offset, grad_input_memory_span);
            safe_add_2d(grad_input.data, iy_se, ix_se, gInp_sH, gInp_sW, inp_H, inp_W, se * gOut, NC_offset, grad_input_memory_span);
          }
          if (in_nw) {
            scalar_t nw_val = inp_ptr[iy_nw * inp_sH + ix_nw * inp_sW + c];
            gix -= static_cast<float>(nw_val) * (iy_se - iy) * static_cast<float>(gOut);
            giy -= static_cast<float>(nw_val) * (ix_se - ix) * static_cast<float>(gOut);
          }
          if (in_ne) {
            scalar_t ne_val = inp_ptr[iy_ne * inp_sH + ix_ne * inp_sW + c];
            gix += static_cast<float>(ne_val) * (iy_sw - iy) * static_cast<float>(gOut);
            giy -= static_cast<float>(ne_val) * (ix - ix_sw) * static_cast<float>(gOut);
          }
          if (in_sw) {
            scalar_t sw_val = inp_ptr[iy_sw * inp_sH + ix_sw * inp_sW + c];
            gix -= static_cast<float>(sw_val) * (iy - iy_ne) * static_cast<float>(gOut);
            giy += static_cast<float>(sw_val) * (ix_ne - ix) * static_cast<float>(gOut);
          }
          if (in_se) {
            scalar_t se_val = inp_ptr[iy_se * inp_sH + ix_se * inp_sW + c];
            gix += static_cast<float>(se_val) * (iy - iy_nw) * static_cast<float>(gOut);
            giy += static_cast<float>(se_val) * (ix - ix_nw) * static_cast<float>(gOut);
          }
          NC_offset += 1;
        }
      }

      scalar_t *gGrid_ptr = grad_grid.data + index * gGrid_sW;
      gGrid_ptr[0] = static_cast<scalar_t>(gix_mult * gix);
      gGrid_ptr[1] = static_cast<scalar_t>(giy_mult * giy);
    }
  }  // for v (VEC_W)
}

// Note [Passing pointer and offset to fastAtomicAdd]
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// For its internal bounds checking, fastAtomicAdd needs to know where the destination address
// lies relative to the entire tensor, so we pass the base grad_input.data and full offset information,
// including batch * channel offset (NC_offset).

template <typename scalar_t, typename index_t>
C10_LAUNCH_BOUNDS_1(1024)
__global__ void grid_sampler_2d_backward_impl_kernel(
    const index_t nthreads,
    TensorInfo<scalar_t, index_t> grad_output,        // N C H_out, W_out, grad_output.shape: [N, C, H_out, W_out] 作为输出
    TensorInfo<scalar_t, index_t> input,              // N C H_in, W_in, input.shape: [N, C, H_in, W_in] 作为输入
    TensorInfo<scalar_t, index_t> grid,               // N H_out, W_out 2, grid.shape: [N, H_out, W_out, 2] 作为输入
    TensorInfo<scalar_t, index_t> grad_input,         // N C H_in, W_in, grad_input.shape: [N, C, H_in, W_in] 作为输出，初始化为0（或如果input_requires_grad为false则不使用）
    TensorInfo<scalar_t, index_t> grad_grid,          // N H_out, W_out 2, grad_grid.shape: [N, H_out, W_out, 2] 作为输出，初始化为空
    const GridSamplerInterpolation interpolation_mode,
    const GridSamplerPadding padding_mode,
    bool align_corners,
    const index_t grad_input_memory_span,
    const bool input_requires_grad) {

  index_t C = input.sizes[1];
  index_t inp_H = input.sizes[2];
  index_t inp_W = input.sizes[3];
  index_t out_H = grid.sizes[1];
  index_t out_W = grid.sizes[2];
  index_t inp_sN = input.strides[0];
  index_t inp_sC = input.strides[1];
  index_t inp_sH = input.strides[2];
  index_t inp_sW = input.strides[3];
  index_t grid_sN = grid.strides[0];
  index_t grid_sH = grid.strides[1];
  index_t grid_sW = grid.strides[2];
  index_t grid_sCoor = grid.strides[3];
  index_t gOut_sN = grad_output.strides[0];
  index_t gOut_sC = grad_output.strides[1];
  index_t gOut_sH = grad_output.strides[2];
  index_t gOut_sW = grad_output.strides[3];
  // gInp_* (and NC_offset below) are not really needed if input_requires_grad is false.
  index_t gInp_sN;
  index_t gInp_sC;
  index_t gInp_sH;
  index_t gInp_sW;
  if (input_requires_grad) {
    gInp_sN = grad_input.strides[0];
    gInp_sC = grad_input.strides[1];
    gInp_sH = grad_input.strides[2];
    gInp_sW = grad_input.strides[3];
  }
  index_t gGrid_sW = grad_grid.strides[2];
  constexpr int VEC_W = 4;  // W方向向量化：每个线程处理4个相邻的w位置
  const index_t w_base = (blockIdx.x * blockDim.x + threadIdx.x) * VEC_W;
  const index_t h_out = blockIdx.y * blockDim.y + threadIdx.y;
  const index_t n = blockIdx.z;

  if (h_out < out_H) {
    // 向量化加载 grid：相邻 VEC_W 个 w 时用 float4 x2 加载 (x0,y0,...,x3,y3)
    scalar_t x_arr[4] = {0}, y_arr[4] = {0};
    const bool can_vec_grid = (w_base + 3 < out_W && grid_sW == 2 && grid_sCoor == 1 &&
                                sizeof(scalar_t) == sizeof(float));
    if (can_vec_grid) {
      const float4 *grid_ptr = reinterpret_cast<const float4*>(
          grid.data + n * grid_sN + h_out * grid_sH + w_base * grid_sW);
      const float4 g0 = grid_ptr[0];
      const float4 g1 = grid_ptr[1];
      x_arr[0] = static_cast<scalar_t>(g0.x);  y_arr[0] = static_cast<scalar_t>(g0.y);
      x_arr[1] = static_cast<scalar_t>(g0.z);  y_arr[1] = static_cast<scalar_t>(g0.w);
      x_arr[2] = static_cast<scalar_t>(g1.x);  y_arr[2] = static_cast<scalar_t>(g1.y);
      x_arr[3] = static_cast<scalar_t>(g1.z);  y_arr[3] = static_cast<scalar_t>(g1.w);
    }

    #pragma unroll
    for (int v = 0; v < VEC_W; ++v) {
      const index_t w = w_base + v;
      if (w >= out_W) break;

      const index_t index = n * out_H * out_W + h_out * out_W + w;
      const auto grid_offset = n * grid_sN + h_out * grid_sH + w * grid_sW;

      scalar_t x, y;
      if (can_vec_grid) {
        x = x_arr[v];
        y = y_arr[v];
      } else {
        x = grid.data[grid_offset];
        y = grid.data[grid_offset + grid_sCoor];
      }

      // multipliers for gradients on ix and iy
      scalar_t gix_mult, giy_mult;
      scalar_t ix = grid_sampler_compute_source_index_set_grad(x, inp_W, padding_mode, align_corners, &gix_mult);
      scalar_t iy = grid_sampler_compute_source_index_set_grad(y, inp_H, padding_mode, align_corners, &giy_mult);

      if (interpolation_mode == GridSamplerInterpolation::Bilinear) {
        index_t ix_nw = static_cast<index_t>(std::floor(ix));
        index_t iy_nw = static_cast<index_t>(std::floor(iy));
        index_t ix_ne = ix_nw + 1;
        index_t iy_ne = iy_nw;
        index_t ix_sw = ix_nw;
        index_t iy_sw = iy_nw + 1;
        index_t ix_se = ix_nw + 1;
        index_t iy_se = iy_nw + 1;

        scalar_t nw = (ix_se - ix)    * (iy_se - iy);
        scalar_t ne = (ix    - ix_sw) * (iy_sw - iy);
        scalar_t sw = (ix_ne - ix)    * (iy    - iy_ne);
        scalar_t se = (ix    - ix_nw) * (iy    - iy_nw);

        bool in_nw = within_bounds_2d(iy_nw, ix_nw, inp_H, inp_W);
        bool in_ne = within_bounds_2d(iy_ne, ix_ne, inp_H, inp_W);
        bool in_sw = within_bounds_2d(iy_sw, ix_sw, inp_H, inp_W);
        bool in_se = within_bounds_2d(iy_se, ix_se, inp_H, inp_W);

        scalar_t gix = static_cast<scalar_t>(0), giy = static_cast<scalar_t>(0);
        const scalar_t *gOut_ptr_NCHW = grad_output.data + n * gOut_sN + h_out * gOut_sH + w * gOut_sW;
        index_t NC_offset = n * gInp_sN;
        const scalar_t *inp_ptr_NC = input.data + n * inp_sN;

        for (index_t c = 0; c < C; ++c, inp_ptr_NC += inp_sC, NC_offset += gInp_sC, gOut_ptr_NCHW += gOut_sC) {
          const scalar_t gOut = *gOut_ptr_NCHW;

          if (input_requires_grad) {
            safe_add_2d(grad_input.data, iy_nw, ix_nw, gInp_sH, gInp_sW, inp_H, inp_W, nw * gOut, NC_offset, grad_input_memory_span);
            safe_add_2d(grad_input.data, iy_ne, ix_ne, gInp_sH, gInp_sW, inp_H, inp_W, ne * gOut, NC_offset, grad_input_memory_span);
            safe_add_2d(grad_input.data, iy_sw, ix_sw, gInp_sH, gInp_sW, inp_H, inp_W, sw * gOut, NC_offset, grad_input_memory_span);
            safe_add_2d(grad_input.data, iy_se, ix_se, gInp_sH, gInp_sW, inp_H, inp_W, se * gOut, NC_offset, grad_input_memory_span);
          }

          if (in_nw) {
            scalar_t nw_val = inp_ptr_NC[iy_nw * inp_sH + ix_nw * inp_sW];
            gix -= nw_val * (iy_se - iy) * gOut;
            giy -= nw_val * (ix_se - ix) * gOut;
          }
          if (in_ne) {
            scalar_t ne_val = inp_ptr_NC[iy_ne * inp_sH + ix_ne * inp_sW];
            gix += ne_val * (iy_sw - iy) * gOut;
            giy -= ne_val * (ix - ix_sw) * gOut;
          }
          if (in_sw) {
            scalar_t sw_val = inp_ptr_NC[iy_sw * inp_sH + ix_sw * inp_sW];
            gix -= sw_val * (iy - iy_ne) * gOut;
            giy += sw_val * (ix_ne - ix) * gOut;
          }
          if (in_se) {
            scalar_t se_val = inp_ptr_NC[iy_se * inp_sH + ix_se * inp_sW];
            gix += se_val * (iy - iy_nw) * gOut;
            giy += se_val * (ix - ix_nw) * gOut;
          }
        }

        scalar_t *gGrid_ptr_NHW = grad_grid.data + index * gGrid_sW;
        gGrid_ptr_NHW[0] = gix_mult * gix;
        gGrid_ptr_NHW[1] = giy_mult * giy;
      }
    } 
  }
}


bool canUse32BitIndexMath(const TensorBase& t, int64_t max_elem) {
  auto elements = t.sym_numel();
  if (elements >= max_elem) {
    return false;
  }
  if (elements == 0) {
    return max_elem > 0;
  }

  c10::SymInt offset = 0;
  auto linearId = elements - 1;

  // NOTE: Assumes all strides are positive, which is true for now
  for (auto i = t.dim() - 1; i >= 0; --i) {
    auto curDimIndex = linearId % t.sym_size(i);
    auto curDimOffset = curDimIndex * t.sym_stride(i);
    offset += curDimOffset;
    linearId /= t.sym_size(i);
  }

  if (offset >= max_elem) {
    return false;
  }

  return true;
}

void launch_grid_sampler_2d_forward_kernel(
  const TensorBase &output, const TensorBase &input, const TensorBase &grid,
  int64_t interpolation_mode, int64_t padding_mode, bool align_corners) {
  // See NOTE [ grid_sampler Native Functions ].
  // Add checks here in case this is called instead of grid_sampler.
  check_grid_sampler_common(input, grid);
  check_grid_sampler_2d(input, grid);
  auto N = input.size(0);
  auto H = grid.size(1);
  auto W = grid.size(2);
  int64_t count = N * H * W;

  bool use_nhwc_kernel = (input.is_contiguous(at::MemoryFormat::ChannelsLast) &&
  output.is_contiguous(at::MemoryFormat::ChannelsLast) &&
  static_cast<GridSamplerInterpolation>(interpolation_mode) == GridSamplerInterpolation::Bilinear);

  if (count > 0) {
    AT_DISPATCH_FLOATING_TYPES_AND2(
      ScalarType::Half, ScalarType::BFloat16,
      input.scalar_type(), "grid_sampler_2d_cuda", [&] {
      if (use_nhwc_kernel) {
        constexpr int BLOCK_H = 8;
        constexpr int BLOCK_W = 32;
        dim3 block(BLOCK_W, BLOCK_H);
        dim3 grid_dims((W + BLOCK_W - 1) / BLOCK_W, (H + BLOCK_H - 1) / BLOCK_H, N);
        if (canUse32BitIndexMath(input) && canUse32BitIndexMath(grid) &&
            canUse32BitIndexMath(output)) {
          grid_sampler_2d_impl_kernel_nhwc<scalar_t>
            <<<grid_dims, block, 0, at::cuda::getCurrentCUDAStream()>>>(
              static_cast<int>(H), static_cast<int>(W),
              getTensorInfo<scalar_t, int>(input),
              getTensorInfo<scalar_t, int>(grid),
              getTensorInfo<scalar_t, int>(output),
              static_cast<GridSamplerInterpolation>(interpolation_mode),
              static_cast<GridSamplerPadding>(padding_mode),
              align_corners);
          C10_CUDA_KERNEL_LAUNCH_CHECK();
        } else {
          grid_sampler_2d_impl_kernel_nhwc<scalar_t>
            <<<grid_dims, block, 0, at::cuda::getCurrentCUDAStream()>>>(
              H, W,
              getTensorInfo<scalar_t, int64_t>(input),
              getTensorInfo<scalar_t, int64_t>(grid),
              getTensorInfo<scalar_t, int64_t>(output),
              static_cast<GridSamplerInterpolation>(interpolation_mode),
              static_cast<GridSamplerPadding>(padding_mode),
              align_corners);
          C10_CUDA_KERNEL_LAUNCH_CHECK();
        }
      } else {
        if (canUse32BitIndexMath(input) && canUse32BitIndexMath(grid) &&
            canUse32BitIndexMath(output)) {
          grid_sampler_2d_impl_kernel<scalar_t>
            <<<GET_BLOCKS(count, 256), 256, 0, at::cuda::getCurrentCUDAStream()>>>(
              static_cast<int>(count),
              getTensorInfo<const scalar_t, int>(input),
              getTensorInfo<const scalar_t, int>(grid),
              getTensorInfo<scalar_t, int>(output),
              static_cast<GridSamplerInterpolation>(interpolation_mode),
              static_cast<GridSamplerPadding>(padding_mode),
              align_corners);
          C10_CUDA_KERNEL_LAUNCH_CHECK();
        } else {
          grid_sampler_2d_impl_kernel<scalar_t>
            <<<GET_BLOCKS(count, 256), 256, 0, at::cuda::getCurrentCUDAStream()>>>(
              count,
              getTensorInfo<const scalar_t, int64_t>(input),
              getTensorInfo<const scalar_t, int64_t>(grid),
              getTensorInfo<scalar_t, int64_t>(output),
              static_cast<GridSamplerInterpolation>(interpolation_mode),
              static_cast<GridSamplerPadding>(padding_mode),
              align_corners);
          C10_CUDA_KERNEL_LAUNCH_CHECK();
        }
      }
    });
  }
}

void launch_grid_sampler_2d_backward_kernel(
    const TensorBase &grad_input, const TensorBase &grad_grid,
    const TensorBase &grad_output, const TensorBase &input,
    const TensorBase &grid, int64_t interpolation_mode, int64_t padding_mode,
    bool align_corners, std::array<bool,2> output_mask) {
  // See NOTE [ grid_sampler Native Functions ].
  // Add checks here in case this is called instead of grid_sampler.
  check_grid_sampler_common(input, grid);
  check_grid_sampler_2d(input, grid);
  // See Note [Writing Nondeterministic Operations]
  // Nondeterministic because of atomicAdd usage
  globalContext().alertNotDeterministic("grid_sampler_2d_backward_cuda");
  auto N = input.size(0);
  auto H = grid.size(1); // output H
  auto W = grid.size(2); // output W

  // If `input` gradient is not required, we skip computing it -- not needing to create
  // the tensor to hold the gradient can markedly increase performance. (`grid` gradient
  // is always computed.)
  auto input_requires_grad = output_mask[0];
  bool use_nhwc_backward = (
    input.is_contiguous(at::MemoryFormat::ChannelsLast) &&
    grad_output.is_contiguous(at::MemoryFormat::ChannelsLast) &&
    grad_input.is_contiguous(at::MemoryFormat::ChannelsLast));

  int64_t count = N * H * W;
  if (count > 0) {
    AT_DISPATCH_FLOATING_TYPES_AND2(
      ScalarType::Half, ScalarType::BFloat16,
      input.scalar_type(), "grid_sampler_2d_backward_cuda", [&] {
      if (canUse32BitIndexMath(input) && canUse32BitIndexMath(grid) &&
          canUse32BitIndexMath(grad_output)) {
        if (use_nhwc_backward) {
          constexpr int BLOCK_H = 2;
          constexpr int BLOCK_W = 256;
          constexpr int VEC_W = 4;  // 每线程处理 4 个 w，grid 向量化读取
          dim3 block_bwd(BLOCK_W, BLOCK_H);
          dim3 grid_bwd((W + VEC_W * BLOCK_W - 1) / (VEC_W * BLOCK_W), (H + BLOCK_H - 1) / BLOCK_H, N);
          grid_sampler_2d_backward_impl_kernel_nhwc<scalar_t>
            <<<grid_bwd, block_bwd, 0, at::cuda::getCurrentCUDAStream()>>>(
              static_cast<int>(H), static_cast<int>(W),
              getTensorInfo<scalar_t, int>(grad_output),
              getTensorInfo<scalar_t, int>(input),
              getTensorInfo<scalar_t, int>(grid),
              input_requires_grad ? getTensorInfo<scalar_t, int>(grad_input) : TensorInfo<scalar_t, int>(),
              getTensorInfo<scalar_t, int>(grad_grid),
              static_cast<GridSamplerInterpolation>(interpolation_mode),
              static_cast<GridSamplerPadding>(padding_mode),
              align_corners,
              input_requires_grad ? static_cast<int>(grad_input.numel()) : 0,
              input_requires_grad);
          C10_CUDA_KERNEL_LAUNCH_CHECK();
        } else {
          constexpr int VEC_W = 4;  // W方向向量化：每线程处理4个w位置
          constexpr int BLOCK_X = 8; //4性能差
          constexpr int BLOCK_Y = 64;
          // 每线程处理 VEC_W 个w，故 x 方向 block 数减半
          dim3 block_dims(BLOCK_X, BLOCK_Y);
          dim3 grid_dims((W + VEC_W * BLOCK_X - 1) / (VEC_W * BLOCK_X), (H + BLOCK_Y - 1) / BLOCK_Y, N);
          grid_sampler_2d_backward_impl_kernel<scalar_t>
          <<<grid_dims, block_dims, 0, at::cuda::getCurrentCUDAStream()>>>(
            static_cast<int>(count),
            getTensorInfo<scalar_t, int>(grad_output),       // N C H W, grad_output.shape: [N, C, H_out, W_out] 作为输出
            getTensorInfo<scalar_t, int>(input),             // N C H_in W_in
            getTensorInfo<scalar_t, int>(grid),              // N H_out W_out 2 
            input_requires_grad ? getTensorInfo<scalar_t, int>(grad_input) : TensorInfo<scalar_t, int>(),
            getTensorInfo<scalar_t, int>(grad_grid),         // N H_out W_out 2 作为输出
            static_cast<GridSamplerInterpolation>(interpolation_mode),
            static_cast<GridSamplerPadding>(padding_mode),
            align_corners,
            /*grad_input_memory_span =*/input_requires_grad ? static_cast<int>(grad_input.numel()) : 0,
            input_requires_grad);
          C10_CUDA_KERNEL_LAUNCH_CHECK();
        }
      } else {
        grid_sampler_2d_backward_impl_kernel<scalar_t>
          <<<GET_BLOCKS(count, 256), 256, 0, at::cuda::getCurrentCUDAStream()>>>(
            count,
            getTensorInfo<scalar_t, int64_t>(grad_output),
            getTensorInfo<scalar_t, int64_t>(input),
            getTensorInfo<scalar_t, int64_t>(grid),
            input_requires_grad ? getTensorInfo<scalar_t, int64_t>(grad_input) : TensorInfo<scalar_t, int64_t>(),
            getTensorInfo<scalar_t, int64_t>(grad_grid),
            static_cast<GridSamplerInterpolation>(interpolation_mode),
            static_cast<GridSamplerPadding>(padding_mode),
            align_corners,
            /*grad_input_memory_span =*/input_requires_grad ? grad_input.numel() : 0,
            input_requires_grad);
        C10_CUDA_KERNEL_LAUNCH_CHECK();
      }
    });
  }
}


Tensor grid_sampler_2d_cuda(const Tensor& input, const Tensor& grid,
                            int64_t interpolation_mode, int64_t padding_mode,
                            bool align_corners) {
  auto in_size = input.sizes();
  auto grid_size = grid.sizes();
  Tensor output = Tensor();
  if (input.is_contiguous(at::MemoryFormat::ChannelsLast)) {
    output = at::empty(
        {in_size[0], in_size[1], grid_size[1], grid_size[2]}, input.options().memory_format(at::MemoryFormat::ChannelsLast));
  } else {
    output = at::empty(
        {in_size[0], in_size[1], grid_size[1], grid_size[2]}, input.options());
  }

  launch_grid_sampler_2d_forward_kernel(
      output, input, grid, interpolation_mode, padding_mode, align_corners);
  return output;
}


std::tuple<Tensor, Tensor>
grid_sampler_2d_backward_cuda(const Tensor& grad_output, const Tensor& input,
                              const Tensor& grid, int64_t interpolation_mode, int64_t padding_mode,
                              bool align_corners, std::array<bool, 2> output_mask) {
  auto input_requires_grad = output_mask[0];
  Tensor grad_input = ([&]() {
    if (input_requires_grad) {
      if (input.is_contiguous(at::MemoryFormat::ChannelsLast)) {
        return at::zeros_like(input, at::MemoryFormat::ChannelsLast);
      } else {
        return at::zeros_like(input, LEGACY_CONTIGUOUS_MEMORY_FORMAT);
      }
    } else {
      return Tensor();
    }
  })();
  auto grad_grid = at::empty_like(grid, LEGACY_CONTIGUOUS_MEMORY_FORMAT);
  launch_grid_sampler_2d_backward_kernel(
      grad_input, grad_grid, grad_output, input,
      grid, interpolation_mode, padding_mode, align_corners, output_mask);
  return std::make_tuple(grad_input, grad_grid);
}


Tensor grid_sample_forward(const Tensor& input, const Tensor& grid, int64_t interpolation_mode, int64_t padding_mode, bool align_corners) {
  return grid_sampler_2d_cuda(input, grid, interpolation_mode, padding_mode, align_corners);
}

std::tuple<Tensor, Tensor> grid_sample_backward(const Tensor& grad_output, const Tensor& input, const Tensor& grid, int64_t interpolation_mode, int64_t padding_mode, bool align_corners, std::array<bool, 2> output_mask) {
  return grid_sampler_2d_backward_cuda(grad_output, input, grid, interpolation_mode, padding_mode, align_corners, output_mask);
}

}
}
